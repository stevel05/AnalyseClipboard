AppType=JavaFX
Build1=Default,com.stevel05.analyseclipboard
File1=hv.bjl
File2=Layout1.bjl
File3=Left.bjl
File4=Right.bjl
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
FileGroup4=Default Group
Group=Default Group
Library1=javaobject
Library10=jbitmapcreator
Library2=jcore
Library3=jfx
Library4=jhexviewer-b4x
Library5=jokhttputils2
Library6=jrandomaccessfile
Library7=jxui
Library8=webp
Library9=xui views
Module1=|absolute|D:\AnywhereSoftware\B4j\aaaIDELinks\aaaIDElinks
Module2=HexViewForm
Module3=LogU
Module4=UserOptions
NumberOfFiles=4
NumberOfLibraries=10
NumberOfModules=4
Version=10.2
@EndOfDesignText@
#Region Project Attributes 
	#MainFormWidth: 900
	#MainFormHeight: 600 
#End Region

#PackagerProperty: IncludedModules = javafx.web
'#CustomBuildAction: After Packager, %WINDIR%\System32\robocopy.exe, ..\Objects\b4xlibs\Files temp\build\bin\ webp.dll
'
''My specific Actions
'#CustomBuildAction: After Packager, %WINDIR%\System32\robocopy.exe, /MIR temp\build\ C:\noinst\B4j\analyseClipboard


Sub Process_Globals
	Private fx As JFX
	Private MainForm As Form
	Private xui As XUI 

	Private SplitPane1 As SplitPane
	Private btnAnalyse As B4XView
	Private WebView1 As WebView
	Private ImageView1 As ImageView
	Private TextArea1 As TextArea
	Private ListView1 As ListView
	Private Clipboard As JavaObject
	Private Dialog As B4XDialog
	Private lblStatus As B4XView
	Private DataFormat As JavaObject
	Private RegisteredFormats As Map
	Private IVWidth As Double = 300
	Private IVHeight As Double = 200
	Private taLog As B4XView
	Private ScrollPane1 As ScrollPane
	Private HvForm As HexViewForm
	Private HexBufferMap As Map
	Private AppName As String = "AnalyseClipboard"
End Sub


Sub AppStart (Form1 As Form, Args() As String)
	xui.SetDataFolder(AppName)
	UserOptions.LoadUserOptions(xui.DefaultFolder,"opts.dat")
	
	MainForm = Form1
	MainForm.RootPane.LoadLayout("Layout1")
	
	MainForm.Title = "Analyse Clipboard"
	
	Dialog.Initialize(MainForm.RootPane)
	
	SplitPane1.LoadLayout("Left")
	SplitPane1.LoadLayout("Right")
	
	Dim L As List = Array(0.66)
	SplitPane1.DividerPositions = UserOptions.ListToDoubleArray(UserOptions.GetDefault("spdividers",L))
	
	MainForm.Show
	
	HvForm.Initialize
	
	ImageView1.RemoveNodeFromParent
	ScrollPane1.InnerNode = ImageView1
	ScrollPane1.FitToHeight = True
	ScrollPane1.FitToWidth = True
	
	DataFormat.InitializeStatic("javafx.scene.input.DataFormat")
	
	RegisteredFormats.Initialize
	Dim DFI As JavaObject
	DFI.InitializeStatic("javafx.scene.input.DataFormat")
	Dim DataFormats As List = Array("FILES","HTML","IMAGE","PLAIN_TEXT","RTF","URL")
	
	For Each DF As String In DataFormats
		RegisteredFormats.Put(DF,DFI.GetField(DF))
	Next
	
	Dim Divider1PosProp As JavaObject = SplitPane1.As(JavaObject).RunMethodJO("getDividers",Null).RunMethodJO("get", Array(0)).RunMethod("positionProperty",Null)
	Dim O As Object = Divider1PosProp.CreateEventFromUI("javafx.beans.value.ChangeListener","Divider1PosPropChanged",Null)
	Divider1PosProp.RunMethod("addListener",Array(O))
	
	HexBufferMap.Initialize
	
End Sub

Public Sub MainForm_CloseRequest (EventData As Event)
	UserOptions.Put("spdividers", UserOptions.DoubleArrayToList(SplitPane1.DividerPositions))
	UserOptions.SaveFormMetrics(MainForm,"")
	UserOptions.SaveUserOptions
End Sub

Public Sub MainForm_Resize(Width As Double, Height As Double)
	ResizeRight(Width, Height)
End Sub

Private Sub Divider1PosPropChanged_Event (MethodName As String, Args() As Object)
	ResizeRight(MainForm.Width,MainForm.Height)
End Sub

Private Sub ResizeRight(Width As Double, Height As Double)
	ImageView1.SetLayoutAnimated(0,0,0,IVWidth,IVHeight)
	Dim RWidth As Double = Width - Width * SplitPane1.DividerPositions(0)
	ScrollPane1.As(B4XView).SetLayoutAnimated(0,0,0,RWidth,Height)
	WebView1.As(B4XView).SetLayoutAnimated(0,0,0,RWidth,Height)
	taLog.SetLayoutAnimated(0,0,0,RWidth,Height)
End Sub


Private Sub btnAnalyse_Click
	
	Clipboard.InitializeStatic("javafx.scene.input.Clipboard")
	Clipboard = Clipboard.RunMethod("getSystemClipboard",Null)
	
	ListView1.Items.Clear
	
	Dim Set As JavaObject = Clipboard.RunMethod("getContentTypes",Null)

	Dim Arr() As Object = Set.RunMethod("toArray",Null)

	Dim L As List = Arr
		
	For Each DF As JavaObject In L
		Dim Set1 As JavaObject = DF.RunMethod("getIdentifiers",Null)
		Dim Arr() As Object = Set1.RunMethod("toArray",Null)
		For Each S As String In Arr
			Dim Lbl As Label = XUIViewsUtils.CreateLabel
			Lbl.Text = S
			Lbl.TooltipText = S
			ListView1.Items.Add(Lbl)
		Next
	Next
	
End Sub

Private Sub TextArea1_Action
	lblStatus.Text = ""
	Dim MI As MenuItem = Sender
	If MI.Text = "Selected" Then
		lblStatus.TextColor = xui.Color_Black
		If TextArea1.SelectionStart = TextArea1.SelectionEnd Then
			lblStatus.Text = "No text selected"
			Return
		End If
		Dim Str As String = TextArea1.Text.SubString2(TextArea1.SelectionStart,TextArea1.SelectionEnd)
		taLog.Text = LogU.GetStringCharNames(Str)
		
	Else If MI.Text = "All Characters" Then
		taLog.BringToFront
		taLog.Text = LogU.GetStringCharNames(TextArea1.Text)
		
	Else If MI.Text = "Hex View" Then
		Dim Selected As String = ListView1.SelectedItem.As(B4XView).Text
		If HexBufferMap.ContainsKey(Selected) Then
			HvForm.Show(HexBufferMap.Get(Selected))
		Else
			HvForm.Show(TextArea1.Text.GetBytes("UtF-8"))
		End If
	End If
			
End Sub

Private Sub ListView1_SelectedIndexChanged(Index As Int)
	WebView1.LoadHtml("")
	TextArea1.Text = ""
	lblStatus.Text = ""
	taLog.Text = ""
	ImageView1.SetImage(Null)
	HexBufferMap.Clear
	
	If Index = -1 Then Return
	Dim ContentType As String = ListView1.Items.Get(Index).As(B4XView).Text

	Dim NoViewer As Boolean

	Dim DF As JavaObject
	If RegisteredFormats.ContainsKey(ContentType) Then
		DF = RegisteredFormats.Get(ContentType)
	Else
		Try
			DF.InitializeNewInstance("javafx.scene.input.DataFormat",Array(Array As String(ContentType)))
			RegisteredFormats.Put(ContentType,DF)
		Catch
			DF = DataFormat.RunMethod("lookupMimeType",Array(ContentType))
			If DF.IsInitialized Then RegisteredFormats.Put(ContentType, DF)
		End Try
	End If
	 
	If DF.IsInitialized = False Then
		Dialog.Title = "Unknown DataFormat"
		Wait For(Dialog.Show($"Cannot determine dataformat for ${ContentType}"$,"OK","","")) Complete (Resp As Int)
		Return
	End If
	
	Select ContentType

		Case "text/plain"
			If DF.IsInitialized = False Then
				DF = DataFormat.GetField("PLAIN_TEXT")
			End If
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))

		Case "System.String"
			If DF.IsInitialized = False Then
				DF = DataFormat.GetField("PLAIN_TEXT")
			End If
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))

		Case "text/rtf"
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))

		Case "RTF As Text"
			If DF.IsInitialized = False Then
				DF = DataFormat.GetField("PLAIN_TEXT")
			End If
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))

		Case "RTF in UTF8"
			If DF.IsInitialized = False Then
				DF = DataFormat.GetField("PLAIN_TEXT")
			End If
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))

		Case "text/uri-list"
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))
			If Clipboard.RunMethod("hasUrl",Null) Then
				Dim URL As String = Clipboard.RunMethod("getUrl",Null)
				
				If URL.StartsWith("file:") Then Return
				
				If URL.ToLowerCase.EndsWith(".webp") Then
					Wait For(Download(URL)) Complete (Data() As Byte)
					
					Dim WP As WebP
					WP.Initialize
					ScrollPane1.As(B4XView).BringToFront
					Dim img As Image = WP.LoadWebP(Data)
					IVWidth = img.Width
					IVHeight = img.Height
					ImageView1.SetLayoutAnimated(0,0,0,IVWidth,IVHeight)
					ImageView1.SetImage(img)
					Return
				End If
				
				Wait For(Download(URL)) Complete (uContent As Object)

				If uContent Is Image Or GetType(uContent) = "anywheresoftware.b4j.objects.ImageViewWrapper$ImageWrapper" Then
					ScrollPane1.As(B4XView).BringToFront
					Dim img As Image
					If uContent Is Image Then
						img = uContent
					Else
						img = uContent.As(JavaObject).RunMethod("getObject",Null)
					End If
					
					IVWidth = img.Width
					IVHeight = img.Height
					If IVWidth = 0 And IVHeight = 0 Then
						lblStatus.Text = "Downloaded Image has no dimensions"
						lblStatus.TextColor = xui.Color_Red
						Return
					End If
					ImageView1.SetLayoutAnimated(0,0,0,IVWidth,IVHeight)
					ImageView1.SetImage(img)
				End If
				
				If uContent Is String And uContent <> "" Then
					WebView1.As(B4XView).BringToFront
					WebView1.LoadHtml(uContent)
				End If
				
			Else If Clipboard.RunMethod("hasFiles",Null) Then
				Try
					Dim L As List = Clipboard.RunMethod("getFiles",Null)
					If L.Size > 0 Then
						Dim URL As String = L.Get(0)
						Wait For(Download(URL)) Complete (Content As String)
						If Content <> "" Then
							WebView1.As(B4XView).BringToFront
							WebView1.LoadHtml(Content)
						End If
					End If
				Catch
					Log(LastException)
				End Try
				
			End If

		Case "text/html"
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))
			WebView1.As(B4XView).BringToFront
			WebView1.LoadHtml(Clipboard.RunMethod("getContent",Array(DF)))

		Case "application/x-java-rawimage"
			ScrollPane1.As(B4XView).BringToFront
			TextArea1.Text = Clipboard.RunMethod("getContent",Array(DF))
			If Clipboard.RunMethod("hasImage",Null) Then
				Try
					Dim img As Image = Clipboard.RunMethod("getImage",Null)
				Catch
					lblStatus.Text = LastException.Message
					lblStatus.TextColor = xui.Color_Red
					Return
				End Try
				IVWidth = img.Width
				IVHeight = img.Height
				If IVWidth = 0 And IVHeight = 0 Then
					lblStatus.Text = "Downloaded Image has no dimensions"
					lblStatus.TextColor = xui.Color_Red
					Return
				End If
				ImageView1.SetImage(img)
				ImageView1.SetLayoutAnimated(0,0,0,IVWidth,IVHeight)
			Else
				lblStatus.Text = "Image not found"
				lblStatus.TextColor = xui.Color_Red
			End If

		Case Else
			Dim ContentObject As Object = Clipboard.RunMethod("getContent",Array(DF))
			If GetType(ContentObject) = "java.nio.HeapByteBuffer" Then
				
				Dim Result() As Byte = getBufferObject(ContentObject)
				If Result = Null Then
					TextArea1.Text = "No Content"
					Return
				End If
				If Result.Length = 0 Then
					TextArea1.Text = ContentObject
					Return
				End If
				HexBufferMap.Put(ContentType, Result)

				Dim img As Image = BytesToImage(Result)
				
				If Initialized(img) And img.Width > 0 And img.Height > 0 Then
					ScrollPane1.As(B4XView).BringToFront
					IVWidth = img.Width
					IVHeight = img.Height
					ImageView1.SetImage(img)
					ImageView1.SetLayoutAnimated(0,0,0,IVWidth,IVHeight)
					TextArea1.Text = ContentObject
					Return
				End If

				If Result.Length > 100000 Then
					lblStatus.Text = "Buffer too large"
					lblStatus.TextColor = xui.Color_Red
					TextArea1.Text = ContentObject
				Else				
				TextArea1.Text = BytesToString(Result,0,Result.Length,"UTF-8")
				End If

			Else If ContentObject Is String Then
				TextArea1.Text = ContentObject
			Else
				TextArea1.Text = ContentObject
				NoViewer = True
			End If
	End Select
	
	If NoViewer Then
		lblStatus.Text = $"Cannot display content ${ContentType}"$
		lblStatus.TextColor = xui.Color_Red
	End If
	
End Sub

Public Sub BytesToImage(bytes() As Byte) As Image
	Dim In As InputStream
	In.InitializeFromBytesArray(bytes, 0, bytes.Length)
	Dim bmp As Image
	bmp.Initialize2(In)
	In.Close
	Return bmp
End Sub

Private Sub getBufferObject(Buffer As Object) As Byte()
	
	Buffer.As(JavaObject).RunMethod("rewind",Null)
	Dim Remaining As Int = Buffer.As(JavaObject).RunMethod("remaining",Null)
	Dim Content(Remaining) As Byte
	Buffer.As(JavaObject).RunMethod("get",Array(Content))
	Return Content
End Sub

Private Sub Download(Url As String) As ResumableSub
	Dim Job As HttpJob
	Dim Content As String
	Job.Initialize("Job", Me)
	Job.Download(Url)

	Wait For (Job) JobDone(jj As HttpJob)
	If Job.Success Then
		If Url.ToLowerCase.EndsWith(".webp") Then
			Dim Bytes(Job.GetInputStream.BytesAvailable) As Byte
			Job.GetInputStream.ReadBytes(Bytes,0,Bytes.Length)
			Job.Release
			Return Bytes
		End If
		Try
			Dim Img As Image = Job.GetBitmap
			If Img.IsInitialized Then 
				Job.Release
				Return Img
			End If
		Catch
			Log(LastException)
		End Try
		
		Content = Job.GetString
	Else
		Dialog.Title = "Error Loading Page"
		Wait For(Dialog.Show("Could not get page content for " & Url, "OK","","")) Complete (Resp As Int)
	End If
	
	Job.Release
	Return Content
End Sub
